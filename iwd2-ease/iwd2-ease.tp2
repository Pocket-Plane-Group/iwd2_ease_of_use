BACKUP ~weidu_external/iwd2-ease/backup~ // where should we put backup files? 
SUPPORT ~https://forums.pocketplane.net/index.php?board=43.0~

ALWAYS
  ACTION_IF NOT VARIABLE_IS_SET cd_iwd2_ease_always THEN BEGIN // check to make this happen only once per install
    OUTER_SET cd_iwd2_ease_always = 1
    INCLUDE ~iwd2-ease/lib/always.tpa~
  END
END  

VERSION ~v16~

README ~iwd2-ease/readme-iwd2-ease.html~
  
AUTO_TRA ~iwd2-ease/languages/%s~

LANGUAGE 
  ~American English~ 
  ~english~ 
  ~iwd2-ease/languages/english/weidu.tra~
  ~iwd2-ease/languages/english/game.tra~
LANGUAGE 
  ~Traducción al Español (hecha por Bhasbuto, Clan DLAN)~ 
  ~spanish~
  ~iwd2-ease/languages/english/weidu.tra~
  ~iwd2-ease/languages/english/game.tra~
  ~iwd2-ease/languages/spanish/weidu.tra~
  ~iwd2-ease/languages/spanish/game.tra~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Bugfixes                                         \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @0 DESIGNATED 0 DEPRECATED @1003

// everything that was here is in IWD2 Fixpack or IWD2EE

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Infinite Stacking                                \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @1 DESIGNATED 1
REQUIRE_PREDICATE GAME_IS ~iwd2~ @1000
REQUIRE_PREDICATE ((!MOD_IS_INSTALLED ~setup-cdtweaks.tp2~ ~3080~) AND    // Unlimited Ammo Stacks AND 
                   (!MOD_IS_INSTALLED ~setup-cdtweaks.tp2~ ~3090~) AND    // Unlimited Gem and Jewelry Stacking AND 
                   (!MOD_IS_INSTALLED ~setup-cdtweaks.tp2~ ~3100~) AND    // Unlimited Potion Stacking AND 
                   (!MOD_IS_INSTALLED ~setup-cdtweaks.tp2~ ~3110~)) @1002 // Unlimited Scroll Stacking
LABEL ~ww_iwd2_eou_infinte_stacking~

// code essentially stolen and adapted from Tweaks Anthology
OUTER_SET stack = 9999
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~    ~override~ // check all items
  READ_SHORT  0x1c "type" ELSE 0
  READ_SHORT  0x38 "max"  ELSE 0
  PATCH_IF (
             ("%max%" > 1) AND // if item can already stack and is of the type...
             (
               ("%type%" =  5) OR // arrows,
               ("%type%" =  9) OR // potions,
               ("%type%" = 11) OR // scrolls,
               ("%type%" = 14) OR // bullets,
               ("%type%" = 16) OR // dagger
               ("%type%" = 21) OR // hammer (iwd2)
               ("%type%" = 24) OR // darts,
               ("%type%" = 25) OR // axe,
               ("%type%" = 29) OR // spears,
               ("%type%" = 31) OR // bolts,
               ("%type%" = 34) OR // gems
               ("%type%" = 71) OR // rations
               ("%SOURCE_RES%" STRING_COMPARE_CASE "00misc31" = 0) OR // lamp oil
               ("%SOURCE_RES%" STRING_COMPARE_CASE "00misc32" = 0)    // flaming oil
             )
           ) BEGIN
    READ_SHORT 0x38 current
    PATCH_IF (stack > current) BEGIN
      WRITE_SHORT  0x38 stack
    END
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Non-Combat War Chant of the Sith                 \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @2 DESIGNATED 2
REQUIRE_PREDICATE GAME_IS ~iwd2~ @1000
REQUIRE_PREDICATE !MOD_IS_INSTALLED ~iwd2ee.tp2~ ~0~ @1002 // class revisions already makes this non-combat
LABEL ~ww_iwd2_eou_non-_combat_war_chant~

// small change from v15: war chant still wn't apply to deafened party members or non-allies
COPY_EXISTING ~spin148.spl~ ~override~
  WRITE_BYTE 0x27 13 // secondary: non-combat
  LPF DELETE_EFFECT INT_VAR match_opcode = 206 match_parameter2 = 81 END // remove 206 for 'not near enemies'
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Improved Holy Avenger                            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @3 DESIGNATED 3
REQUIRE_PREDICATE GAME_IS ~iwd2~ @1000
REQUIRE_PREDICATE !MOD_IS_INSTALLED ~iwd2ee.tp2~ ~4~ @1001 // iwd2ee radically changes this
LABEL ~ww_iwd2_eou_holy_avenger_improved~

ACTION_IF subs BEGIN
  OUTER_SPRINT ha_match   @2001
  OUTER_SPRINT ha_replace @2002
END    

COPY_EXISTING ~60hfslha.itm~ ~override~
              ~60swdlha.itm~ ~override~
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode = 402 target = 2 parameter2 = 61 timing = 1 resist_dispel = 2 STR_VAR resource = ~effuhc1~ END // +2d6 vs. chaotic
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode = 402 target = 2                 timing = 1 resist_dispel = 2 STR_VAR resource = ~effdm1~  END // dispel magic vs. everyone
  PATCH_IF subs BEGIN // for english, sub text in the description directly
    READ_STRREF 0x54 desc
    PATCH_IF ("%desc%" STRING_COMPARE_REGEXP ~<Invalid Strref -?[0-9]+>~) BEGIN // sanity check, checking for valid content in string
      INNER_PATCH_SAVE desc ~%desc%~ BEGIN
        REPLACE_TEXTUALLY ~%ha_match%~ ~%ha_replace%~
      END
      SAY_EVALUATED 0x54 ~%desc%~
    END
  END ELSE BEGIN // otherwise use static descriptions
    PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_CASE "60swdlha" = 0) BEGIN
      SAY DESC @4
    END ELSE BEGIN  
      SAY DESC @5
    END
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Two-Handed Greatsword Holy Avenger               \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @6 DESIGNATED 4
REQUIRE_PREDICATE GAME_IS ~iwd2~ @1000
LABEL ~ww_iwd2_eou_holy_avenger_2h~

ACTION_IF subs BEGIN
  OUTER_SPRINT dam_match   @2003
  OUTER_SPRINT dam_replace @2004
  OUTER_SPRINT hd1_match   @2005
  OUTER_SPRINT hd1_replace @2006
  OUTER_SPRINT feat_match   @2007
  OUTER_SPRINT feat_replace @2008
END    

COPY_EXISTING ~60hfslha.itm~ ~override~
              ~60swdlha.itm~ ~override~
  WRITE_LONG  0x18 THIS | BIT1 // add two-handed flag
  WRITE_SHORT 0x1c 57          // category: great swords
  WRITE_ASCII 0x22 "S2"        // inventory icon
  LPF ALTER_HEADER_IWD2 INT_VAR match_type = 1 range = 2 dicenumber = 2 dicesize = 6 END // range 2, base damage 2d6
  PATCH_IF subs BEGIN // for english, sub text in the description directly
    READ_STRREF 0x54 desc
    PATCH_IF ("%desc%" STRING_COMPARE_REGEXP ~<Invalid Strref -?[0-9]+>~) BEGIN // sanity check, checking for valid content in string
      INNER_PATCH_SAVE desc ~%desc%~ BEGIN
        REPLACE_TEXTUALLY ~%dam_match%~  ~%dam_replace%~  // replace damagae line with 2d6
        REPLACE_TEXTUALLY ~%hd1_match%~  ~%hd1_replace%~  // replace one-handed line with two-handed
        REPLACE_TEXTUALLY ~%feat_match%~ ~%feat_replace%~ // replace 'large sword' with 'great sword' in feat line
      END
      SAY_EVALUATED 0x54 ~%desc%~
    END  
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// No-Fists for Iron Body                           \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @7 DESIGNATED 5
REQUIRE_PREDICATE GAME_IS ~iwd2~ @1000
REQUIRE_PREDICATE !MOD_IS_INSTALLED ~iwd2ee.tp2~ ~2~ @1002 // spell revisions already makes this change
LABEL ~ww_iwd2_eou_iron_body_no_fist~

COPY_EXISTING ~spwi807.spl~ ~override~ // iron body
  LPF DELETE_EFFECT INT_VAR match_opcode = 111 END // delete opcode that creates fist
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Deep Gnome Starting XP                           \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @8 DESIGNATED 6
REQUIRE_PREDICATE GAME_IS ~iwd2~ @1000
REQUIRE_PREDICATE !MOD_IS_INSTALLED ~iwd2ee.tp2~ ~84~ @1001 // iwd2ee radically changes this
LABEL ~ww_iwd2_eou_gnome_starting_xp~

COPY_EXISTING ~strtxp.2da~ ~override~
  REPLACE_TEXTUALLY ~^\(GNOME_DEEP[ %TAB%]+\)[0-9]+~ ~\19000~
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Improved Moonblades                              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @9 DESIGNATED 7
REQUIRE_PREDICATE GAME_IS ~iwd2~ @1000
REQUIRE_PREDICATE !MOD_IS_INSTALLED ~iwd2ee.tp2~ ~4~ @1001 // iwd2ee radically changes this
LABEL ~ww_iwd2_eou_improved_moonblades~

ACTION_IF subs BEGIN
  OUTER_SPRINT dam_match    @2009
  OUTER_SPRINT dam_replace  @2010
  OUTER_SPRINT atk_match    @2011
  OUTER_SPRINT feat_match   @2007
  OUTER_SPRINT feat_replace @2012
END    

COPY_EXISTING ~00swdl09.itm~   ~override~
  WRITE_SHORT 0x1c 19   // short sword
  LPF ALTER_HEADER_IWD2 INT_VAR match_type = 1 to_hit = 0 END // remove +2 to-hit
  LPF ALTER_EFFECT INT_VAR match_opcode = 12 parameter1 = 4 dicenumber = 1 dicesize = 12 END // was 1d8+2 magic damage, now 1d12+4
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode = 60 target = 2 power = 3 parameter1 = 100 parameter2 = 2 resist_dispel = 1 duration = 7 END // 100% casting failure for one round
  PATCH_IF subs BEGIN // for english, sub text in the description directly
    READ_STRREF 0x50 desc
    PATCH_IF ("%desc%" STRING_COMPARE_REGEXP ~<Invalid Strref -?[0-9]+>~) BEGIN // sanity check, checking for valid content in string
      INNER_PATCH_SAVE desc ~%desc%~ BEGIN
        REPLACE_TEXTUALLY ~%dam_match%~  ~%dam_replace%~  // replace damagae line with 1d12+4
        REPLACE_TEXTUALLY ~%atk_match%~  ~~               // remove attack bonus line
        REPLACE_TEXTUALLY ~%feat_match%~ ~%feat_replace%~ // replace 'large sword' with 'small blade' in feat line
      END
      SAY_EVALUATED 0x50 ~%desc%~
    END
  END
  BUT_ONLY
  
COPY_EXISTING ~moonbla.itm~   ~override~
  WRITE_SHORT 0x1c 19   // short sword
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Improved Monk Fists                              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @10 DESIGNATED 8
REQUIRE_PREDICATE GAME_IS ~iwd2~ @1000
LABEL ~ww_iwd2_eou_improved_monk_fists~

COPY_EXISTING_REGEXP GLOB ~^00mfist[1-9]\.itm$~ ~override~
  WRITE_SHORT 0x1c 19 // short sword
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Collector's Edition Bonus Items                  \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @11 DESIGNATED 9
REQUIRE_PREDICATE GAME_IS ~iwd2~ @1000
LABEL ~ww_iwd2_eou_ce_items~

EXTEND_BOTTOM ~baldur.bcs~ ~iwd2-ease/baf/ce.baf~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// No Alignment Class Restrictions                  \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @12 DESIGNATED 10
REQUIRE_PREDICATE GAME_IS ~iwd2~ @1000
REQUIRE_PREDICATE !MOD_IS_INSTALLED ~iwd2ee.tp2~ ~1~ @1001 // different approach
LABEL ~ww_iwd2_eou_no_class_align_restrictions~

COPY_EXISTING ~alignmnt.2da~ ~override~
  REPLACE_TEXTUALLY 
    ~^\([^ %TAB%]+\)[ %TAB%]+[0-9][ %TAB%]+[0-9][ %TAB%]+[0-9][ %TAB%]+[0-9][ %TAB%]+[0-9][ %TAB%]+[0-9][ %TAB%]+[0-9][ %TAB%]+[0-9][ %TAB%]+[0-9]~
    ~\1 1 1 1 1 1 1 1 1 1~
  PRETTY_PRINT_2DA
  BUT_ONLY  

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Magic Weapon Finesse                             \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @13 DESIGNATED 11
REQUIRE_PREDICATE GAME_IS ~iwd2~ @1000
LABEL ~ww_iwd2_eou_magic_weapon_finesse~

COPY_EXISTING_REGEXP GLOB ~^bbodisa\.itm$~         ~override~
                          ~^ltouch\.itm$~          ~override~
                          ~^gtouch\.itm$~          ~override~
                          ~^harm\.itm$~            ~override~
                          ~^destruc\.itm$~         ~override~
                          ~^mswrd[0-9][0-9]\.itm$~ ~override~
  WRITE_SHORT 0x1c 19 // short sword
  BUT_ONLY  

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Always Get Some XP Per Kill                      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @14 DESIGNATED 12
REQUIRE_PREDICATE GAME_IS ~iwd2~ @1000
REQUIRE_PREDICATE ((!MOD_IS_INSTALLED ~iwd2ee.tp2~ ~42~) AND (!!MOD_IS_INSTALLED ~iwd2ee.tp2~ ~43~)) @1001 // different approach
LABEL ~ww_iwd2_eou_always_some_xp~

COPY    ~iwd2-ease/2da/moncrate.2da~   ~override~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Some Heart of Fury Items In Normal Mode          \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @15 DESIGNATED 13
REQUIRE_PREDICATE GAME_IS ~iwd2~ @1000
LABEL ~ww_iwd2_eou_hof_items_in_regular~

COPY_EXISTING ~rt_fury.2da~     ~override/rt_norm.2da~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Stronger Bastard Swords                          \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @16 DESIGNATED 14
REQUIRE_PREDICATE GAME_IS ~iwd2~ @1000
REQUIRE_PREDICATE !MOD_IS_INSTALLED ~iwd2ee.tp2~ ~4~ @1001 // different approach
LABEL ~ww_iwd2_eou_stronger_bastard_swords~

COPY ~iwd2-ease/itm/00swdbbs.itm~   ~override~
  SAY DESC @17

COPY ~iwd2-ease/itm/00hfsbbs.itm~   ~override~
  SAY DESC @18

COPY ~iwd2-ease/itm/00swdbrc.itm~   ~override~
  SAY DESC @19

COPY ~iwd2-ease/itm/00hfsbrc.itm~   ~override~
  SAY DESC @20

COPY ~iwd2-ease/itm/00swdbwr.itm~   ~override~
  SAY DESC @21

COPY ~iwd2-ease/itm/00hfsbwr.itm~   ~override~
  SAY DESC @22

COPY ~iwd2-ease/itm/00swdb91.itm~   ~override~
     ~iwd2-ease/itm/00swdb91.itm~   ~override/00swdb98.itm~
  SAY DESC @23

COPY ~iwd2-ease/itm/00swdb92.itm~   ~override~
  SAY DESC @24

COPY ~iwd2-ease/itm/00swdb93.itm~   ~override~
  SAY DESC @25

COPY ~iwd2-ease/itm/00swdb96.itm~   ~override~
  SAY DESC @26

COPY ~iwd2-ease/itm/00swdb97.itm~   ~override~
  SAY DESC @27

COPY ~iwd2-ease/itm/00swdb99.itm~   ~override~
  SAY DESC @28

COPY ~iwd2-ease/spl/effzzj6.spl~ ~override~
COPY ~iwd2-ease/itm/zzj6bs.itm~  ~override~
  SAY DESC @29

COPY_EXISTING ~11deird.sto~ ~override~
  ADD_STORE_ITEM ~00swdb99~ AFTER ~00swdb02~ #0 #0 #0 ~IDENTIFIED~ #1 // insert fang after masterwork bastard sword

// add chaos-on-hit, equipped-chaotic-commands dynamically to Order's Nemesis and Rage of Chaos
ACTION_FOR_EACH sword IN 00swdbrc 00hfsbrc BEGIN
  LAF cd_items_casting_spells INT_VAR cast_at_level = 1 duration = 21 item_header = 1 probability = 100 
                              STR_VAR item  = EVAL ~%sword%~ spell = spwi509 END
  LAF cd_items_casting_spells INT_VAR cast_at_level = 1 item_header = 0 probability = 100 
                              STR_VAR item  = EVAL ~%sword%~ spell = sppr508 END
                              
  COPY_EXISTING ~%sword%.itm~ ~override~ // fix params on newly-added global effects from chaotic commands
    LPF DELETE_EFFECT INT_VAR check_headers = 0 match_opcode = 174 END
    LPF ALTER_EFFECT INT_VAR check_headers = 0 power = 0 resist_dispel = 0 END // bypass spell protections
    LPF ALTER_EFFECT INT_VAR check_headers = 0 match_timing = 0 timing = 2 duration = 0 END // change to equipped, ignore one-off visuals
    
END  

ACTION_IF MOD_IS_INSTALLED ~iwd2ee.tp2~ ~0~ BEGIN // compat with iwd2ee class revisions

  OUTER_SET splstate_ME_ITEM_TYPE_EQUIPPED = IDS_OF_SYMBOL (~splstate~ ~ME_ITEM_TYPE_EQUIPPED~)

  COPY_EXISTING ~00swdbbs.itm~   ~override~ // Bastard's Son
                ~00hfsbbs.itm~   ~override~ // Know Thy Family
                ~00swdbrc.itm~   ~override~ // Rage of Chaos
                ~00hfsbrc.itm~   ~override~ // Order's Nemesis
                ~00swdbwr.itm~   ~override~ // Wroth
                ~00hfsbwr.itm~   ~override~ // Bloody Wroth
                ~00swdb91.itm~   ~override~ // Icy Bastard Sword +1
                ~00swdb92.itm~   ~override~ // Static Bastard Sword
                ~00swdb93.itm~   ~override~ // Bastard Sword of Heroism
                ~00swdb96.itm~   ~override~ // Bastard Sword +3: Cold Fire
                ~00swdb97.itm~   ~override~ // Bastard Sword +2: Black Adder
                ~00swdb98.itm~   ~override~ // Bastard Sword of Frost +1
                ~00swdb99.itm~   ~override~ // fang
                ~zzj6bs.itm~     ~override~ // Bastard Sword of the Undead Slayer +1
    SET 288_save = (BIT18 + BIT19 + BIT21 + BIT22)                
//    PATCH_IF (("%SOURCE_RES%" STRING_COMPARE_CASE "00swdbrc" = 0) OR 
//              ("%SOURCE_RES%" STRING_COMPARE_CASE "00hfsbrc" = 0) OR 
//              ("%SOURCE_RES%" STRING_COMPARE_CASE "00swdbwr" = 0) OR 
//              ("%SOURCE_RES%" STRING_COMPARE_CASE "00hfsbwr" = 0)) BEGIN SET 288_save = 288_save + BIT20 // add cursed flag
    LPF ADD_ITEM_EQEFFECT INT_VAR insert_point = 0 opcode = 287 target = 1 timing = 2 STR_VAR resource = USNONSNA END
    LPF ADD_ITEM_EQEFFECT INT_VAR insert_point = 1 opcode = 288 target = 1 parameter1 = 69 parameter2 = splstate_ME_ITEM_TYPE_EQUIPPED timing = 2 savingthrow = 288_save special = 5 STR_VAR resource = USEQTYPE END
    LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode = 500 target = 2 special = 1 STR_VAR resource = MEONHIT END
    LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode = 500 target = 2 parameter2 = 16777216 savingthrow = (BIT14 + BIT19 + BIT23 + BIT24) STR_VAR resource = EXDAMAGE END
  
  // convert cold damage to iwd2ee  
  COPY_EXISTING ~00swdb91.itm~   ~override~ // Icy Bastard Sword +1
                ~00swdb98.itm~   ~override~ // Bastard Sword of Frost +1
    LPF ALTER_EFFECT INT_VAR match_opcode = 12 opcode = 500 parameter1 = 0x00050600 parameter2 = 131072 savingthrow = BIT16 timing = 0 dicesize = 0 dicenumber = 0 STR_VAR resource = EXDAMAGE END
    
  // convert electrical damage to iwd2ee  
  COPY_EXISTING ~00swdb92.itm~   ~override~ // Static Bastard Sword
    LPF ALTER_EFFECT INT_VAR match_opcode = 12 opcode = 500 parameter1 = 0x00050600 parameter2 = 262144 savingthrow = BIT16 timing = 0 dicesize = 0 dicenumber = 0 STR_VAR resource = EXDAMAGE END
    
  // convert cold, fire damage to iwd2ee  
  COPY_EXISTING ~00swdb96.itm~   ~override~ // Bastard Sword +3: Cold Fire
    LPF ALTER_EFFECT INT_VAR match_opcode = 12 multi_match = 1 opcode = 500 parameter1 = 0x00030600 parameter2 = 131072 savingthrow = BIT16 timing = 0 dicesize = 0 dicenumber = 0 STR_VAR resource = EXDAMAGE END
    LPF ALTER_EFFECT INT_VAR match_opcode = 12                 opcode = 500 parameter1 = 0x00030600 parameter2 = 524288 savingthrow = BIT16 timing = 0 dicesize = 0 dicenumber = 0 STR_VAR resource = EXDAMAGE END
    
  // convert acid damage to iwd2ee  
  COPY_EXISTING ~00swdb99.itm~   ~override~ // fang
    LPF ALTER_EFFECT INT_VAR match_opcode = 12 opcode = 500 parameter1 = 0x00050600 parameter2 = 65536 savingthrow = BIT16 timing = 0 dicesize = 0 dicenumber = 0 STR_VAR resource = EXDAMAGE END

END
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// All Items Identified                             \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @30 DESIGNATED 15
REQUIRE_PREDICATE GAME_IS ~iwd2~ @1000
REQUIRE_PREDICATE !MOD_IS_INSTALLED ~setup-cdtweaks.tp2~ ~3020~ @1002 // same as tweaks anthology
LABEL ~ww_iwd2_eou_id_all_items~

// if you can't steal code from yourself, who can you steal it from?

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    WRITE_SHORT 0x42 0
  END
  BUT_ONLY

OUTER_SET itm_off_cre = 0x616 // cre v2.2
OUTER_SET itm_off_are = 0x88  // are v9.1

COPY_EXISTING_REGEXP GLOB ~^.+\.[ac]re$~ ~override~
  PATCH_IF ("%SOURCE_FILE%" STRING_COMPARE_REGEXP "^.+\.cre$" = 0) BEGIN // if creature
    READ_LONG (itm_off_cre       ) itm_off ELSE 0
    READ_LONG (itm_off_cre + 0x04) itm_num ELSE 0
  END ELSE BEGIN
    READ_LONG  (itm_off_are       ) itm_off ELSE 0
    READ_SHORT (itm_off_are - 0x02) itm_num ELSE 0
  END
  FOR (index = 0 ; index < itm_num ; ++index) BEGIN
    WRITE_BYTE (itm_off + 0x10 + (0x14 * index)) (THIS BOR 0b00000001) // adds identified flag
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Non-Linear Teleportation                         \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @31 DESIGNATED 16
REQUIRE_PREDICATE GAME_IS ~iwd2~ @1000
LABEL ~ww_iwd2_eou_non_linear_teleportation~

EXTEND_BOTTOM ~dplayer3.bcs~ ~iwd2-ease/baf/teleport.baf~

ACTION_IF !FILE_EXISTS_IN_GAME ~multig.dlg~ BEGIN COMPILE ~iwd2-ease/dlg/teleport_multig.d~ END // create multig in case it doesn't exist
COMPILE ~iwd2-ease/dlg/teleport.d~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Armor and Shields Provide Damage Resistance      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @32 DESIGNATED 17
REQUIRE_PREDICATE GAME_IS ~iwd2~ @1000
REQUIRE_PREDICATE !MOD_IS_INSTALLED ~iwd2ee.tp2~ ~39~ @1001 // different approach
LABEL ~ww_iwd2_eou_armor_shield_damage_resistance~
  
ACTION_IF subs BEGIN
  OUTER_SPRINT res_match   @2020
  OUTER_SPRINT res_txt     @2021
  OUTER_SPRINT cdeol       @2022
END

// this was originally done by copying in a bunch of items and overwriting existing stuff. 
// After examination, the new items simply had four ops (86-89) that provided resistance bonuses 
// equal to their shield/armor AC bonus. So, we regexp it instead:
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  SET ac = 0
  READ_LONG 0x6a fx_off
  READ_LONG 0x70 fx_num
  FOR (index = 0 ; index < fx_num ; ++index) BEGIN
    READ_SHORT (fx_off +        (index * 0x30)) op
    PATCH_IF op = 0 BEGIN
      READ_LONG (fx_off + 0x08 + (index * 0x30)) ac_type
      PATCH_IF ((ac_type = 1) OR (ac_type = 3)) BEGIN
        READ_LONG (fx_off + 0x04 + (index * 0x30)) ac
      END 
    END
  END    
  PATCH_IF ac > 0 BEGIN // ac o armor/shield found, now adjust existing resistanec or add them 
    PATCH_IF subs BEGIN
      READ_LONG 0x54 desc
      PATCH_IF (desc > 0) AND (desc < 999999) BEGIN
        SET offset = 0x54
      END ELSE BEGIN
        SET offset = 0x50
      END
      READ_STRREF offset desc
      PATCH_IF ("%desc%" STRING_COMPARE_REGEXP ~<Invalid Strref -?[0-9]+>~) BEGIN // sanity check, checking for valid content in string
        INNER_PATCH_SAVE desc ~%desc%~ BEGIN
          REPLACE_TEXTUALLY ~%res_match%%ac%%cdeol%~ ~\1%ac% (%ac%%res_txt%)\3~
        END
        SAY_EVALUATED ~%offset%~ ~%desc%~ // write changes
      END
    END
    SET slash = 0
    SET crush = 0
    SET pierce = 0
    SET missile = 0
    FOR (index = 0 ; index < fx_num ; ++index) BEGIN
      READ_SHORT (fx_off +        (index * 0x30)) op
      PATCH_IF ((op > 85) AND (op < 90)) BEGIN
        READ_LONG (fx_off +        (index * 0x30)) param2
        PATCH_IF !param2 BEGIN // ONLY if it's an increment effect
          WRITE_LONG (fx_off + 0x04 + (index * 0x30)) (THIS + ac)
          PATCH_MATCH op WITH // adjust existing effects, if present
            86 BEGIN SET slash = 1   END
            87 BEGIN SET crush = 1   END
            88 BEGIN SET pierce = 1  END
            DEFAULT  SET missile = 1 
          END
        END          
      END 
    END
    PATCH_IF !slash BEGIN
      LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 86 target = 1 parameter1 = ac timing = 2 END
    END  
    PATCH_IF !crush BEGIN
      LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 87 target = 1 parameter1 = ac timing = 2 END
    END  
    PATCH_IF !pierce BEGIN
      LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 88 target = 1 parameter1 = ac timing = 2 END
    END  
    PATCH_IF !missile BEGIN
      LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 89 target = 1 parameter1 = ac timing = 2 END
    END  
  END
  BUT_ONLY 

// Examine every creature. If its AC is between 10 and 35 (which includes
// the Guardian but not Wisps) then assume that the game designers were
// using AC to really mean damage resistance (e.g., it should be clear
// that it is fairly easy to HIT a dragon but fairly hard to hurt it). 
// In that case we lower the AC to 10 and put all of those points into 
// damage resistance for that creature. 

// To date (Wed Jan 22 09:25:23  2003), this is my most complicated WeiDU
// expression. 

/* cam: preserving this for future WeiDU historians:
COPY_EXISTING_REGEXP ~.*\.CRE~ ~override~
  READ_SHORT "0x46" "AC"
  READ_SHORT "0x48" "RES1" 
  READ_SHORT "0x4A" "RES2"
  READ_SHORT "0x4C" "RES3"
  READ_SHORT "0x4E" "RES4"

  WRITE_SHORT "0x46" "10"
  WRITE_SHORT "0x48" ("%RES1%" + ("%AC%" - "10"))
  WRITE_SHORT "0x4A" ("%RES2%" + ("%AC%" - "10"))
  WRITE_SHORT "0x4C" ("%RES3%" + ("%AC%" - "10"))
  WRITE_SHORT "0x4E" ("%RES4%" + ("%AC%" - "10"))

  IF_EVAL ( ("%AC%" > "10") AND 
            ("%AC%" <= "35") )
*/

// and now the modern way to do it
// see you in another 20 years
COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
  READ_SHORT 0x46 ac
  PATCH_IF ((ac > 10) AND (ac <= 35)) BEGIN
    WRITE_SHORT 0x46 10 // set AC to 10 and dump the rest into resistance
    FOR (off = 0x5c ; off < 0x60 ; ++off) BEGIN // hits 0x5c through 0x5f--resistance respectively to slash, crush, pierce, and missile
      WRITE_BYTE off ((THIS + (ac - 10)) MODULO 255)
    END
  END 
  BUT_ONLY  

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Include Forgotten Armor and Shields              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @33 DESIGNATED 18
REQUIRE_PREDICATE GAME_IS ~iwd2~ @1000
LABEL ~ww_iwd2_eou_forgotten_armor_and_shields~

COPY_EXISTING ~50tahvo.sto~ ~override~ // Tahvo the Huntmaster
  LPF ADD_STORE_ITEM_EX STR_VAR position = ~AFTER 00leat01~ item_name = ~00hide04~ END // Umber Hulk Plate
  LPF ADD_STORE_ITEM_EX STR_VAR position = ~AFTER 00hide04~ item_name = ~00hide05~ END // Black Dragon Scale
  BUT_ONLY

COPY ~iwd2-ease/sto/w#oswald.sto~ ~override~ // oswald's secret stash
  SAY 0xc @38
  
ACTION_IF !MOD_IS_INSTALLED ~iwd2ee.tp2~ ~36~ BEGIN
    
  COMPILE ~iwd2-ease/dlg/forgot.d~ // shawford, oswald only
    USING ~iwd2-ease/languages/english/forgot.tra~ ~iwd2-ease/languages/%LANGUAGE%/forgot.tra~
  
END ELSE BEGIN  
    
  COMPILE ~iwd2-ease/dlg/forgot.d~           // shawford, oswald only
          ~iwd2-ease/dlg/forgot_iwd2ee_36.d~ // extra shawford transition to catch
    USING ~iwd2-ease/languages/english/forgot.tra~ ~iwd2-ease/languages/%LANGUAGE%/forgot.tra~

END 

// iwd2ee already restores most of these, so need a major fork here
ACTION_IF MOD_IS_INSTALLED ~iwd2ee.tp2~ ~4~ BEGIN

  COPY_EXISTING ~w#oswald.sto~ ~override~ // oswald's secret stash
    LPF DELETE_STORE_ITEM STR_VAR item_to_delete = 00chan07 END
    LPF DELETE_STORE_ITEM STR_VAR item_to_delete = 00leat08 END
    LPF DELETE_STORE_ITEM STR_VAR item_to_delete = 60rthf04 END
    LPF DELETE_STORE_ITEM STR_VAR item_to_delete = 60rthf05 END
    
END ELSE BEGIN

  COPY  ~iwd2-ease/itm/00rtww1.itm~  ~override/00rtar1.itm~
        ~iwd2-ease/itm/00rtww1.itm~  ~override/00rtar2.itm~
        ~iwd2-ease/itm/00rtww1.itm~  ~override/00rtar3.itm~

  APPEND ~rt_norm.2da~
~00RTAR1  12SHLDCT
00RTAR2  12SHLDSG
00RTAR3  12SHLDKD
~	        
  APPEND ~rt_fury.2da~
~00RTAR1  12HFSDCT
00RTAR2  12HFSGSG
00RTAR3  12HFSDKD
~	        

  // 00hide05, 00leat08
  ACTION_IF subs BEGIN // remove line about natural origins/druid usage

    OUTER_SPRINT druid_match @2013  
    COPY_EXISTING ~12shldct.itm~ ~override~ // Cliffs of Targos
                  ~12shldkd.itm~ ~override~ // Knucklehead
                  ~12hfsdct.itm~ ~override~ // Cliffs over Maer Dualdon  
                  ~12hfsdkd.itm~ ~override~ // Apsel's Rolly-Polly Knuckledheaded Shield            
      READ_STRREF 0x54 desc
      INNER_PATCH_SAVE desc ~%desc%~ BEGIN
        REPLACE_TEXTUALLY ~%druid_match%~ ~~
      END
      SAY_EVALUATED 0x54 ~%desc%~
      BUT_ONLY
      
  END ELSE BEGIN 

    COPY_EXISTING ~12shldct.itm~ ~override~
      SAY DESC @34

    COPY_EXISTING ~12hfsdct.itm~ ~override~
      SAY DESC @35

    COPY_EXISTING ~12shldkd.itm~ ~override~
      SAY DESC @36

    COPY_EXISTING ~12hfsdkd.itm~ ~override~
      SAY DESC @37

  END  

  COPY_EXISTING ~10guthe.sto~ ~override~ // Henghelm's Goods
    LPF ADD_STORE_ITEM_EX STR_VAR position = ~AFTER 00shld01~ item_name = ~00shld87~ END // Tower Shield of Spell Resistance
    BUT_ONLY

  COPY_EXISTING ~11deird.sto~ ~override~ // Gallaway Trading Depot
    LPF ADD_STORE_ITEM_EX STR_VAR position = ~AFTER 00leat01~ item_name = ~60rthf06~ END
    BUT_ONLY
    
  COMPILE ~iwd2-ease/dlg/forgot_base.d~ 
    USING ~iwd2-ease/languages/english/forgot.tra~ ~iwd2-ease/languages/%LANGUAGE%/forgot.tra~
      
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Additional Druid Spells                          \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @39 DESIGNATED 19
REQUIRE_PREDICATE GAME_IS ~iwd2~ @1000
LABEL ~ww_iwd2_eou_additional_druid_spells~

// will defer to IWD2EE's Spell Revisions for level placement, but will still add otherwise
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_druid BEGIN  

  ~SPPR106~ => 1 // Magic Stone [already added in IWD2EE spell revisions at same level]
  ~SPPR107~ => 1 // Protection From Evil [already added in IWD2EE spell revisions at same level]
  ~SPPR408~ => 3 // Magic Circle Against Evil [already added in IWD2EE spell revisions at level 4]
  ~SPWI103~ => 1 // Burning Hands [already added in IWD2EE spell revisions at same level]
  ~SPWI107~ => 2 // Eagle's Splendor [already added in IWD2EE spell revisions at same level]
  ~SPWI122~ => 1 // Ice Dagger [already added in IWD2EE spell revisions at same level]
  ~SPWI214~ => 2 // Bull's Strength [already added in IWD2EE spell revisions at same level]
  ~SPWI215~ => 2 // Web [already added in IWD2EE spell revisions at same level]
  ~SPWI223~ => 2 // Cat's Grace [already added in IWD2EE spell revisions at same level]
  ~SPWI318~ => 3 // Icelance [already added in IWD2EE spell revisions at same level]
  ~SPWI408~ => 4 // Stoneskin [already added in IWD2EE spell revisions at same level]
  ~SPWI424~ => 4 // Vitriolic Sphere [already added in IWD2EE spell revisions at same level]
  ~SPWI428~ => 4 // Spider Spawn [already added in IWD2EE spell revisions at same level]
  ~SPWI504~ => 5 // Cone of Cold [already added in IWD2EE spell revisions at same level]
  ~SPWI511~ => 5 // Shroud of Flame [already added in IWD2EE spell revisions at same level]
  ~SPWI519~ => 5 // Sunfire [already added in IWD2EE spell revisions at same level]
  ~SPWI522~ => 5 // Protection from Acid
  ~SPWI523~ => 4 // Protection from Electricity
  ~SPWI525~ => 5 // Ball Lightning [already added in IWD2EE spell revisions at same level]
  ~SPWI603~ => 6 // Chain Lightning [already added in IWD2EE spell revisions at same level]
  ~SPWI605~ => 6 // Acid Fog
  ~SPWI620~ => 6 // Trollish Fortitude [already added in IWD2EE spell revisions at same level]
  ~SPWI626~ => 6 // Wyvern Call
  ~SPWI708~ => 6 // Acid Storm [already added in IWD2EE spell revisions at level 7]
  ~SPWI719~ => 7 // Vipergout
  ~SPWI802~ => 8 // Summon Monster VIII
    
END

COPY_EXISTING ~listspll.2da~ ~override~
  PATCH_PHP_EACH cd_druid AS spell => level BEGIN
    REPLACE_EVALUATE CASE_INSENSITIVE ~^\([0-9]+[ %TAB%]+[0-9]+[ %TAB%]+[0-9]+[ %TAB%]+\)\([0-9]+\)\([ %TAB%]+[0-9]+[ %TAB%]+[0-9]+[ %TAB%]+[0-9]+[ %TAB%]+[0-9]+[ %TAB%]+\)\(%spell%\)~ BEGIN  
      PATCH_IF ~%MATCH2%~ = 0 BEGIN // not already enabled
        SET MATCH2 = level
      END ELSE BEGIN
        DEFINE_ASSOCIATIVE_ARRAY cd_druid BEGIN ~%spell%~ => 0 END // already available, so update array so next steps get skipped
      END  
    END ~%MATCH1%%MATCH2%%MATCH3%%MATCH4%~
  END  
  PRETTY_PRINT_2DA
  BUT_ONLY
  
ACTION_IF subs BEGIN
  OUTER_SPRINT drd_match   @2018
  OUTER_SPRINT drd_txt     @2019
END
  
ACTION_PHP_EACH cd_druid AS spell => level BEGIN

  ACTION_IF level BEGIN // only proceed if this component enabled it in listspll
  
    COPY_EXISTING ~%spell%z.itm~ ~override~ // make scroll usable by druids
      WRITE_LONG 0x1e (THIS BOR BIT3) // adds druid flag
      BUT_ONLY IF_EXISTS
      
    ACTION_IF subs BEGIN // try to update spell descriptions 
    
      COPY_EXISTING ~%spell%.spl~  ~override~
        READ_LONG   0x50 desc_strref
        READ_STRREF 0x50 desc
        INNER_PATCH_SAVE desc ~%desc%~ BEGIN
          REPLACE_TEXTUALLY ~%drd_match%~ ~\1%drd_txt% %level%, ~
        END
        BUT_ONLY
      
      STRING_SET_EVALUATE desc_strref ~%desc%~ // since scrolls & spells use same string, do it as STRING_SET
        
    END
    
  END  
  
END

COPY_EXISTING ~listspll.2da~ ~override~
  COUNT_2DA_ROWS 2 count
  READ_2DA_ENTRY (count - 1) 0 2 entry
  BUT_ONLY

OUTER_SET entry += 1
APPEND ~listspll.2da~ ~%entry% 0 0 2 0 0 0 0 SPPR411~ UNLESS ~[ %TAB%]SPPR411~

COPY_EXISTING ~listspll.2da~ ~override~
  PRETTY_PRINT_2DA
  BUT_ONLY
  
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Alternate Shapeshifting                          \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @40 DESIGNATED 20
REQUIRE_PREDICATE GAME_IS ~iwd2~ @1000
REQUIRE_PREDICATE !MOD_IS_INSTALLED ~iwd2ee.tp2~ ~0~ @1001 // class revisions completely overhauls this
LABEL ~ww_iwd2_eou_alternate_shapeshifting~

COPY_EXISTING ~clabdr01.2da~ ~override~
  COUNT_2DA_COLS cols
  COUNT_2DA_ROWS cols rows
  REPLACE_TEXTUALLY 
    ~[ %TAB%]\(GA_SPIN\(107\|110\|244\|261\|262\|141\|142\|143\|280\)\|FA_\(24895\|24896\)\|AP_EFFREST\)[ %TAB%]~ 
    ~ **** ~ // remove existing shifts, remove tireless body as it's moved one level earlier
  BUT_ONLY
  
OUTER_FOR (row = rows + 1 ; row < 7 ; ++row) BEGIN // pad table if not enough rows
  OUTER_SPRINT newrow ~ABILITY%row%~
  OUTER_FOR (index2 = 1 ; index2 < cols ; ++index2) BEGIN
    OUTER_SPRINT newrow ~%newrow% ****~
  END
  APPEND ~clabdr01.2da~ ~%newrow%~
END  

COPY_EXISTING ~clabdr01.2da~ ~override~
  LPF ADD_CLAB_ENTRY INT_VAR level =  5 STR_VAR entry1 = ~GA_W#DRU01~ END
  LPF ADD_CLAB_ENTRY INT_VAR level =  6 STR_VAR entry1 = ~GA_W#DRU01~ END
  LPF ADD_CLAB_ENTRY INT_VAR level =  7 STR_VAR entry1 = ~GA_W#DRU01~ entry2 = ~GA_W#DRU02~ END
  LPF ADD_CLAB_ENTRY INT_VAR level =  8 STR_VAR entry1 = ~GA_W#DRU01~ entry2 = ~GA_W#DRU02~ entry3 = ~FA_24896~ END
  LPF ADD_CLAB_ENTRY INT_VAR level =  9 STR_VAR entry1 = ~GA_W#DRU01~ entry2 = ~GA_W#DRU02~ entry3 = ~GA_W#DRU03~ END
  LPF ADD_CLAB_ENTRY INT_VAR level = 10 STR_VAR entry1 = ~GA_W#DRU01~ entry2 = ~GA_W#DRU02~ entry3 = ~GA_W#DRU03~ END
  LPF ADD_CLAB_ENTRY INT_VAR level = 11 STR_VAR entry1 = ~GA_W#DRU01~ entry2 = ~GA_W#DRU02~ entry3 = ~GA_W#DRU03~ entry4 = ~GA_W#DRU04~ END
  LPF ADD_CLAB_ENTRY INT_VAR level = 12 STR_VAR entry1 = ~GA_W#DRU01~ entry2 = ~GA_W#DRU02~ entry3 = ~GA_W#DRU03~ entry4 = ~GA_W#DRU04~ END
  LPF ADD_CLAB_ENTRY INT_VAR level = 13 STR_VAR entry1 = ~GA_W#DRU01~ entry2 = ~GA_W#DRU02~ entry3 = ~GA_W#DRU03~ entry4 = ~GA_W#DRU04~ entry5 = ~GA_W#DRU05~ END
  LPF ADD_CLAB_ENTRY INT_VAR level = 14 STR_VAR entry1 = ~FA_24895~   entry2 = ~AP_EFFREST~ entry3 = ~GA_W#DRU02~ entry4 = ~GA_W#DRU03~ entry5 = ~GA_W#DRU04~ entry6 = ~GA_W#DRU05~ END
  LPF ADD_CLAB_ENTRY INT_VAR level = 15 STR_VAR entry1 = ~GA_W#DRU02~ entry2 = ~GA_W#DRU03~ entry3 = ~GA_W#DRU04~ entry4 = ~GA_W#DRU05~ END
  LPF ADD_CLAB_ENTRY INT_VAR level = 16 STR_VAR entry1 = ~GA_W#DRU03~ entry2 = ~GA_W#DRU04~ entry3 = ~GA_W#DRU05~ entry4 = ~GA_W#DRU06~ END
  LPF ADD_CLAB_ENTRY INT_VAR level = 17 STR_VAR entry1 = ~GA_W#DRU03~ entry2 = ~GA_W#DRU04~ entry3 = ~GA_W#DRU05~ entry4 = ~GA_W#DRU06~ entry5 = ~GA_W#DRU07~ END
  LPF ADD_CLAB_ENTRY INT_VAR level = 18 STR_VAR entry1 = ~GA_W#DRU04~ entry2 = ~GA_W#DRU05~ entry3 = ~GA_W#DRU06~ entry4 = ~GA_W#DRU07~ END
  LPF ADD_CLAB_ENTRY INT_VAR level = 19 STR_VAR entry1 = ~GA_W#DRU04~ entry2 = ~GA_W#DRU05~ entry3 = ~GA_W#DRU06~ entry4 = ~GA_W#DRU07~ entry5 = ~GA_W#DRU08~ END
  LPF ADD_CLAB_ENTRY INT_VAR level = 20 STR_VAR entry1 = ~GA_W#DRU05~ entry2 = ~GA_W#DRU06~ entry3 = ~GA_W#DRU07~ entry4 = ~GA_W#DRU08~ END
  LPF ADD_CLAB_ENTRY INT_VAR level = 21 STR_VAR entry1 = ~GA_W#DRU05~ entry2 = ~GA_W#DRU06~ entry3 = ~GA_W#DRU07~ entry4 = ~GA_W#DRU08~ entry5 = ~GA_W#DRU09~ END
  LPF ADD_CLAB_ENTRY INT_VAR level = 22 STR_VAR entry1 = ~GA_W#DRU06~ entry2 = ~GA_W#DRU07~ entry3 = ~GA_W#DRU08~ entry4 = ~GA_W#DRU09~ END
  LPF ADD_CLAB_ENTRY INT_VAR level = 23 STR_VAR entry1 = ~GA_W#DRU06~ entry2 = ~GA_W#DRU07~ entry3 = ~GA_W#DRU08~ entry4 = ~GA_W#DRU09~ END
  LPF ADD_CLAB_ENTRY INT_VAR level = 24 STR_VAR entry1 = ~GA_W#DRU07~ entry2 = ~GA_W#DRU08~ entry3 = ~GA_W#DRU09~ END
  LPF ADD_CLAB_ENTRY INT_VAR level = 25 STR_VAR entry1 = ~GA_W#DRU07~ entry2 = ~GA_W#DRU08~ entry3 = ~GA_W#DRU09~ END
  LPF ADD_CLAB_ENTRY INT_VAR level = 26 STR_VAR entry1 = ~GA_W#DRU08~ entry2 = ~GA_W#DRU09~ END
  LPF ADD_CLAB_ENTRY INT_VAR level = 27 STR_VAR entry1 = ~GA_W#DRU08~ entry2 = ~GA_W#DRU09~ END
  LPF ADD_CLAB_ENTRY INT_VAR level = 28 STR_VAR entry1 = ~GA_W#DRU09~ END
  LPF ADD_CLAB_ENTRY INT_VAR level = 29 STR_VAR entry1 = ~GA_W#DRU09~ END
  PRETTY_PRINT_2DA

COPY ~iwd2-ease/bam/spin107b.bam~ ~override~
COPY_EXISTING ~spin107.spl~ ~override~ // Polar Bear > Remorhaz
  SAY 0x08 @43
  LPF ALTER_EFFECT INT_VAR match_opcode = 135 STR_VAR resource = ~w#dru06~ END
  LPF ALTER_EFFECT INT_VAR match_opcode = 111 STR_VAR resource = ~w#dru06~ END

COPY ~iwd2-ease/bam/spin110b.bam~ ~override~
COPY_EXISTING ~spin110.spl~ ~override~ // Winter Wolf > Fomorian Giant
  SAY 0x08 @42
  LPF DELETE_EFFECT INT_VAR match_opcode = 174 STR_VAR match_resource = wolff04 END // remove wolf howl from transformation
  LPF ALTER_EFFECT  INT_VAR match_opcode = 135 STR_VAR resource = ~w#dru04~ END
  LPF ALTER_EFFECT  INT_VAR match_opcode = 111 STR_VAR resource = ~w#dru04~ END

COPY ~iwd2-ease/bam/spin111b.bam~ ~override~
COPY_EXISTING ~spin111.spl~ ~override~ // Boring Beetle > Will o' Wisp
  SAY 0x08 @50
  LPF ALTER_EFFECT INT_VAR match_opcode = 135 STR_VAR resource = ~w#dru02~ END
  LPF ALTER_EFFECT INT_VAR match_opcode = 111 STR_VAR resource = ~w#dru02~ END

COPY ~iwd2-ease/bam/spin141b.bam~ ~override~
     ~iwd2-ease/cre/w#dru09.cre~  ~override~
COPY_EXISTING ~spin141.spl~ ~override~ // Fire Elemental > Red Half-Dragon
  SAY 0x08 @46
  LPF ALTER_EFFECT INT_VAR match_opcode = 135 STR_VAR resource = ~w#dru09~ END
  LPF ALTER_EFFECT INT_VAR match_opcode = 111 STR_VAR resource = ~w#dru09~ END

COPY ~iwd2-ease/bam/spin142b.bam~ ~override~
COPY_EXISTING ~spin142.spl~ ~override~ // Earth Elemental > Blue Half-Dragon
  SAY 0x08 @47
  LPF ALTER_EFFECT INT_VAR match_opcode = 135 STR_VAR resource = ~w#dru10~ END
  LPF ALTER_EFFECT INT_VAR match_opcode = 111 STR_VAR resource = ~w#dru10~ END

COPY ~iwd2-ease/bam/spin143b.bam~ ~override~
COPY_EXISTING ~spin143.spl~ ~override~ // Water Elemental > Black Half-Dragon
  SAY 0x08 @48
  LPF ALTER_EFFECT INT_VAR match_opcode = 135 STR_VAR resource = ~w#dru11~ END
  LPF ALTER_EFFECT INT_VAR match_opcode = 111 STR_VAR resource = ~w#dru11~ END

COPY ~iwd2-ease/bam/spin244b.bam~ ~override~
COPY_EXISTING ~spin244.spl~ ~override~ // Boar > Verbeeg
  SAY 0x08 @41
  LPF DELETE_EFFECT INT_VAR match_opcode = 174 STR_VAR match_resource = pbear01 END // remove animal growl from transformation
  LPF ALTER_EFFECT INT_VAR match_opcode = 135 STR_VAR resource = ~w#dru01~ END
  LPF ALTER_EFFECT INT_VAR match_opcode = 111 STR_VAR resource = ~w#dru01~ END

COPY ~iwd2-ease/bam/spin245b.bam~ ~override~
COPY_EXISTING ~spin245.spl~ ~override~ // Black Panther > Yeti
  SAY 0x08 @51
  LPF DELETE_EFFECT INT_VAR match_opcode = 174 STR_VAR match_resource = pbear01 END // remove animal growl from transformation
  LPF ALTER_EFFECT INT_VAR match_opcode = 135 STR_VAR resource = ~w#dru03~ END
  LPF ALTER_EFFECT INT_VAR match_opcode = 111 STR_VAR resource = ~w#dru03~ END

COPY ~iwd2-ease/bam/spin246b.bam~ ~override~
COPY_EXISTING ~spin246.spl~ ~override~ // Shambling Mound > Treant
  SAY 0x08 @52
  LPF DELETE_EFFECT INT_VAR match_opcode = 174 STR_VAR match_resource = pbear01 END // remove animal growl from transformation
  LPF ALTER_EFFECT INT_VAR match_opcode = 135 STR_VAR resource = ~w#dru05~ END
  LPF ALTER_EFFECT INT_VAR match_opcode = 111 STR_VAR resource = ~w#dru05~ END

COPY ~iwd2-ease/bam/spin261b.bam~ ~override~
COPY_EXISTING ~spin261.spl~ ~override~ // Dire Bear > Wyvern
  SAY 0x08 @44
  LPF DELETE_EFFECT INT_VAR match_opcode = 174 STR_VAR match_resource = pbear01 END // remove animal growl from transformation
  LPF ALTER_EFFECT INT_VAR match_opcode = 135 STR_VAR resource = ~w#dru07~ END
  LPF ALTER_EFFECT INT_VAR match_opcode = 111 STR_VAR resource = ~w#dru07~ END

COPY ~iwd2-ease/bam/spin262b.bam~ ~override~
COPY_EXISTING ~spin262.spl~ ~override~ // Dire Panther > White Half-Dragon
  SAY 0x08 @45
  LPF DELETE_EFFECT INT_VAR match_opcode = 174 STR_VAR match_resource = pbear01 END // remove animal growl from transformation
  LPF ALTER_EFFECT INT_VAR match_opcode = 135 STR_VAR resource = ~w#dru08~ END
  LPF ALTER_EFFECT INT_VAR match_opcode = 111 STR_VAR resource = ~w#dru08~ END

COPY ~iwd2-ease/bam/spin280b.bam~ ~override~
COPY_EXISTING ~spin280.spl~ ~override~ // Air Elemental > Elemental Half-Dragon
  SAY 0x08 @49
  WRITE_ASCII 0x3a ~spin280b~ // type, using spln280 (L for I)
  LPF ALTER_EFFECT INT_VAR match_opcode = 135 STR_VAR resource = ~w#dru12~ END
  LPF ALTER_EFFECT INT_VAR match_opcode = 111 STR_VAR resource = ~w#dru12~ END

COPY_EXISTING ~spin244.spl~ ~override/w#dru01.spl~
              ~spin110.spl~ ~override/w#dru02.spl~
              ~spin107.spl~ ~override/w#dru03.spl~
              ~spin261.spl~ ~override/w#dru04.spl~
              ~spin262.spl~ ~override/w#dru05.spl~
              ~spin141.spl~ ~override/w#dru06.spl~
              ~spin142.spl~ ~override/w#dru07.spl~
              ~spin143.spl~ ~override/w#dru08.spl~
              ~spin280.spl~ ~override/w#dru09.spl~

COPY ~iwd2-ease/cre/w#dru01.cre~     ~override~ // this batch uses existing iwd2 strings
     ~iwd2-ease/cre/w#dru02.cre~     ~override~
     ~iwd2-ease/cre/w#dru03.cre~     ~override~
     ~iwd2-ease/cre/w#dru04.cre~     ~override~
     ~iwd2-ease/cre/w#dru05.cre~     ~override~
     ~iwd2-ease/cre/w#dru06.cre~     ~override~
     ~iwd2-ease/cre/w#dru07.cre~     ~override~
     ~iwd2-ease/cre/w#dru09.cre~     ~override~
COPY ~iwd2-ease/cre/w#dru08.cre~     ~override~
  SAY 0x08 @2014
  SAY 0x0c @2014
COPY ~iwd2-ease/cre/w#dru10.cre~     ~override~
  SAY 0x08 @2015
  SAY 0x0c @2015
COPY ~iwd2-ease/cre/w#dru11.cre~     ~override~
  SAY 0x08 @2016
  SAY 0x0c @2016
COPY ~iwd2-ease/cre/w#dru12.cre~     ~override~
  SAY 0x08 @2017
  SAY 0x0c @2017
  
COPY ~iwd2-ease/itm/w#dru01.itm~     ~override~
  SAY 0x0c @53  
COPY ~iwd2-ease/itm/w#dru02.itm~     ~override~
  SAY 0x0c @54  
COPY ~iwd2-ease/itm/w#dru03.itm~     ~override~
  SAY 0x0c @55  
COPY ~iwd2-ease/itm/w#dru04.itm~     ~override~
  SAY 0x0c @56  
COPY ~iwd2-ease/itm/w#dru05.itm~     ~override~
  SAY 0x0c @57  
COPY ~iwd2-ease/itm/w#dru06.itm~     ~override~
  SAY 0x0c @58  
COPY ~iwd2-ease/itm/w#dru07.itm~     ~override~ // uses existing string
COPY ~iwd2-ease/itm/w#dru08.itm~     ~override~
  SAY 0x0c @59  
COPY ~iwd2-ease/itm/w#dru09.itm~     ~override~
  SAY 0x0c @60  
COPY ~iwd2-ease/itm/w#dru10.itm~     ~override~
  SAY 0x0c @61
COPY ~iwd2-ease/itm/w#dru11.itm~     ~override~
  SAY 0x0c @62
COPY ~iwd2-ease/itm/w#dru12.itm~     ~override~
  SAY 0x0c @63
  
COPY_EXISTING ~feats.2da~       ~override~
  REPLACE ~37272~ @50 // Boring Beetle > Will o' Wisp
  REPLACE ~37290~ @64
  REPLACE ~37273~ @51 // Black Panther > Yeti
  REPLACE ~37291~ @65
  REPLACE ~37274~ @52 // Shambling Mound > Treant
  REPLACE ~37292~ @66

COPY_EXISTING ~help02.2da~      ~override~
  REPLACE ~9560~ @67 // generic druid description

COPY_EXISTING ~listinnt.2da~ ~override~
  COUNT_2DA_ROWS 2 count
  READ_2DA_ENTRY (count - 1) 0 2 base_entry

OUTER_FOR (index = 1 ; index < 10 ; ++index) BEGIN
  OUTER_SET entry = base_entry + index
  APPEND ~listinnt.2da~ ~%entry% w#dru0%index%~  
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Skip Battle Square                               \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @68 DESIGNATED 21
REQUIRE_PREDICATE GAME_IS ~iwd2~ @1000
LABEL ~ww_iwd2_eou_skip_battle_square~

COMPILE ~iwd2-ease/dlg/41goleic.d~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Faster Oswald                                    \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @70 DESIGNATED 23
REQUIRE_PREDICATE GAME_IS ~iwd2~ @1000
LABEL ~ww_iwd2_eou_faster_oswald~

OUTER_SET multiplier = 0 // set timers to this%, 0% = no time, 50 = 50% (half time)
INCLUDE ~iwd2-ease/lib/faster_oswald.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Randomized Treasure                              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @69 DESIGNATED 22
REQUIRE_PREDICATE GAME_IS ~iwd2~ @1000
LABEL ~ww_iwd2_eou_randomized_treasure~

INCLUDE ~iwd2-ease/lib/random.tpa~

// always keep randomized treasure last in the tp2 for compat reasons
